plugins {
    id 'org.sonarqube' version '2.6.2'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.jfrog.artifactory' version '4.8.1'
    id 'org.ajoberstar.git-publish' version '2.0.0-rc.2'
    id 'net.researchgate.release' version '2.7.0'
    id 'me.champeau.gradle.jmh' version '0.4.7'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'distribution'
apply plugin: 'me.champeau.gradle.jmh'

group 'com.apptastic'
version "${version}"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    jcenter()

    maven {
        url  "https://jcenter.bintray.com"
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.23.0'
}

jmh {
    include = ['.*InsynsregistretBenchmark*']
    humanOutputFile = project.file("${project.buildDir}/reports/jmh/human.txt")
    resultsFile = project.file("${project.buildDir}/reports/jmh/results.csv")

    //benchmarkMode = 'thrpt'//'thrpt' // Benchmark mode. Available modes are: [Throughput/thrpt, AverageTime/avgt, SampleTime/sample, SingleShotTime/ss, All/all]
    forceGC = true // Should JMH force GC between iterations?
    //profilers = ['COMP', 'STACK'] //['COMP','GC', 'STACK','CL'] // Use profilers
    timeUnit = 's' // Output time unit. Available time units are: [m, s, ms, us, ns].
    verbosity = 'NORMAL' // Verbosity mode. Available modes are: [SILENT, NORMAL, EXTRA]

    timeOnIteration = '5s' // Time to spend at each measurement iteration.
    batchSize = 1 // Batch size: number of benchmark method calls per operation. (some benchmark modes can ignore this setting)
    fork = 5 // How many times to forks a single benchmark. Use 0 to disable forking altogether
    iterations = 1 // Number of measurement iterations to do.
    threads = 1 // Number of worker threads to run with.

    warmup = '3s' // Time to spend at each warmup iteration.
    warmupBatchSize = 1 // Warmup batch size: number of benchmark method calls per operation.
    warmupForks = 1 // How many warmup forks to make for a single benchmark. 0 to disable warmup forks.
    warmupIterations = 1 // Number of warmup iterations to do.
    warmupMode = 'INDI' // Warmup mode for warming up selected benchmarks. Warmup modes are: [INDI, BULK, BULK_INDI].
}

distTar {
    compression = Compression.GZIP
    extension = 'tar.gz'
}

distributions {
    main {
        contents {
            from("$projectDir")
            exclude('.gitignore')
            exclude('.travis.yml')
            exclude('*.iml')
            exclude('out')
            exclude('build')
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

ext.moduleName = 'com.apptastic.insynsregistret'

compileJava {
    inputs.property('moduleName', moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}

jacoco {
    toolVersion = "0.8.2"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    if (!project.version.endsWith("-SNAPSHOT")) {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        publications = ['InsynsregistretPublication']
        dryRun = false
        publish = true
        override = true

        pkg {
            repo = 'maven-repo'
            name = 'insynsregistret'
            desc = 'Java library for extracting data from the Financial Supervisory Authority (Finansinspektionen) insider registry.'
            issueTrackerUrl = 'https://github.com/w3stling/insynsregistret/issues'
            vcsUrl = 'https://github.com/w3stling/insynsregistret.git'
            licenses = ['MIT']

            version {
                name = System.properties['version']
                released = new Date()
            }
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory'
    publish {
        repository {
            if (project.version.endsWith("-SNAPSHOT")) {
                repoKey = 'oss-snapshot-local'
            } else {
                repoKey = 'oss-release-local'
            }
            username = System.getenv('ARTIFACTORY_USERNAME')
            password = System.getenv('ARTIFACTORY_PASSWORD')
            maven = true
        }
        defaults {
            publications ('InsynsregistretPublication')
        }
    }
}

publishing {
    publications {
        InsynsregistretPublication(MavenPublication) {
            from components.java
            groupId 'com.apptastic'
            artifactId 'insynsregistret'
            version "${version}"

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

gitPublish {
    repoUri = 'https://github.com/w3stling/insynsregistret.git'
    branch = 'gh-pages'

    contents {
        from("$buildDir/docs/javadoc") {
            into "javadoc/${version}"
        }
    }

    preserve {
        include 'javadoc/**'
        exclude 'javadoc/*-SNAPSHOT/**'
    }

    commitMessage = 'Publishing javadoc'
}

release {
    tagTemplate = 'v$version'
}
